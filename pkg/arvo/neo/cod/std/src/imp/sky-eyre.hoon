/@  eyre-reqs
/-  serv=server
^-  kook:neo
|%
++  state  pro/%eyre-reqs
++  poke   (sy %eyre-task ~)
++  kids
  :+  ~  %y
  %-  ~(gas by *lads:neo)
  ~
++  deps  
  %-  ~(gas by *band:neo)
  ~
::
++  form
  |_  [=bowl:neo =aeon:neo =pail:neo]
  ++  poke
    |=  [=stud:neo vax=vase]
    ^-  (quip card:neo pail:neo)
    ?+    stud  ~|(bad-stud/stud !!)
         %eyre-task
      =+  !<(=task:eyre:neo vax)
      =/  [eyre-id=@ta req=inbound-request:eyre]  task
      =/  lin  (parse-request-line:serv url.request.req)
      =/  inner=pith:neo  #/[p/our.bowl]/sky
      =/  args  (~(gas by *(map @t @t)) args.lin)
      =/  =crew:neo  (~(gas by *crew:neo) src/inner ~)
      :_  sig/!>(~)
      :~  [(welp here.bowl #/[uv/eny.bowl]) %make %sky-eyre-handler `[stud vax] crew]
      ==
    ==
  ++  init
    |=  pal=(unit pail:neo)
    :_  sig/!>(~)
    =/  =pith:neo  #/[p/our.bowl]/$/eyre
    =/  =binding:eyre  [~ ~[%neo %sky]]
    =/  =req:eyre:neo  [%connect binding here.bowl]
    :~  [pith %poke eyre-req/!>(req)]
    ==
  --
--


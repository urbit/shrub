/@  post
^-  kook:neo
|%
++  state  pro/%sig
++  poke  (silt %rely ~)
++  kids
  :+  ~  %y
  %-  ~(gas by *lads:neo)
  :~  :-  [|/%da |]
      [pro/%post ~]
  ==
++  deps
  %-  ~(gas by *deps:neo)
  :~  :-  %pub
      :+  req=|  [pro/%sig (sy %post ~)]
      :+  ~  %y
      %-  ~(gas by *lads:neo)
      :~  :-  [|/%da |]
          [pro/%post ~]
      ==
  ==
++  form
  ^-  form:neo
  |_  [=bowl:neo =aeon:neo state=pail:neo]
  ++  init
    |=  old=(unit pail:neo)
    ^-  (quip card:neo pail:neo)
    :: ~&  >>  p:(~(got by deps.bowl) %pub)
    :: ~&  >>  (kids-at-pith:su q:(~(got by deps.bowl) %pub) /)
    [~ sig/!>(~)]
  ::
  ++  poke
    |=  [=stud:neo vax=vase]
    ^-  (quip card:neo pail:neo)
    ?>  =(%rely stud)
    ~&  >>  'got rely'
    :_  state
    %+  murn  
      ~(tap of:neo q:(~(got by deps.bowl) %pub))
    |=  [=pith:neo =idea:neo]
    ^-  (unit card:neo)
    ?.  ?=([[%da @] ~] pith)
      ~
    ?:  (~(has of:neo kids.bowl) pith)
      ~
    ?.  =(%post p.pail.idea)
      ~
    `[(welp here.bowl pith) %make %post `pail.idea ~]
  --
--

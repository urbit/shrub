/@  dm-diff
/@  message
=>
|%
++  card  card:neo
--
^-  kook:neo
|%
++  state  pro/%sig
++  poke  (sy %dm-diff %rely ~)
++  kids
  :+  ~  %y
  %-  ~(gas by *lads:neo)
  :~  :-  [|/%da |]
      [pro/%message ~]
  ==
++  deps
  %-  ~(gas by *deps:neo)
  :~  
  ::
  :-  %link
  ::
  :+  req=|  [pro/%sig (sy %dm-diff ~)]
  :+  ~  %y
  %-  ~(gas by *lads:neo)
  :~  :-  [|/%da |]
      [pro/%message ~]
    ==
  ==
++  form
  ^-  form:neo
  |_  [=bowl:neo =aeon:neo sta=pail:neo]
  ++  poke
    |=  [=stud:neo vax=vase]
    ^-  (quip card:neo pail:neo)
    ?:  =(%rely stud)
      =+  !<([=term =stem:neo] vax)
      ?>  ?=(%y -.q.stem)
      =/  =pail:neo  pail:(snag 0 ~(val by kids.q.stem))
      =+  !<(=message q.pail)
      ::  TODO handle
      ?<  =(our.bowl from.message)
      :_  sta
      :_  ~  
      :*                               
        (welp here.bowl ~[da/now.bowl])
        %make                 
        [%message `pail ~]
      ==
    ?>  =(%dm-diff stud)
    =/  poke  !<(dm-diff vax)
    ?>  =(our ship.src):bowl
    ?>  =(%msg -.poke)
    :_  sta
    :~  
      :*
        (welp were.bowl ~[da/now.bowl])
        %make 
        [%message `message/!>(message.poke) ~]
      ==
    ==
  ++  init
    |=  old=(unit pail:neo)  
    ^-  (quip card:neo pail:neo)
    `sig/!>(~)
  --
--

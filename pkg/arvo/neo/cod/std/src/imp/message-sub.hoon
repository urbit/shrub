/@  message
^-  kook:neo
|%
++  state  pro/%sig
++  poke  (silt %rely ~)
++  kids
  :+  ~  %y
  %-  ~(gas by *lads:neo)
  :~  :-  [|/%da |]
      [pro/%message ~]
  ==
++  deps
  %-  ~(gas by *deps:neo)
  :~  :-  %pub
      :+  req=|  [pro/%sig (sy %sig ~)]
      :+  ~  %y
      %-  ~(gas by *lads:neo)
      :~  :-  [|/%da |]
          [pro/%message ~]
      ==
  ==
++  form
  ^-  form:neo
  |_  [=bowl:neo =aeon:neo state=pail:neo]
  ++  init
    |=  old=(unit pail:neo)
    ^-  (quip card:neo pail:neo)
    [~ sig/!>(~)]
  ::
  ++  poke
    |=  [=stud:neo vax=vase]
    ^-  (quip card:neo pail:neo)
    ?>  =(%rely stud)
    :_  state
    =+  !<([=term =leaf:neo] vax)
    ::  only get new kids
    %+  murn 
      ~(tap by ~(tar of:neo q:(~(got by deps.bowl) %pub)))
    |=  [=pith:neo =idea:neo]
    ^-  (unit card:neo)
    ~&  pith/pith
    ?.  ?=([[%da @] ~] pith)
      ~
    ?:  (~(has of:neo kids.bowl) pith)
      ~
    ?.  =(%message p.pail.idea)
      ~
    ~&  making/~
    `[(welp here.bowl pith) %make %message `pail.idea ~]
  --
--

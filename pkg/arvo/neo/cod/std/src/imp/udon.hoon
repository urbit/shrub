/@  txt
/-  sail
^-  kook:neo
|%
++  state  pro/%txt
++  poke   (sy %txt ~)
++  kids
  ^-  (unit (pair dare:neo (map pish:neo lash:neo)))
  :-  ~
  :-  %y
  %-  ~(gas by *lads:neo)
  :~
    [[&/%manx |] [pro/%manx (sy %manx ~)]]
    [[&/%tang |] [pro/%tang (sy %tang ~)]]
  ==
++  deps   *deps:neo
++  form
  ^-  form:neo
  |_  [=bowl:neo =saga:neo]
  ++  poke
    |=  =pail:neo
    ^-  (quip card:neo pail:neo)
    ?>  ~|(incompatible-stud/p.pail =(p.pail %txt))
    =/  code  !<(txt q.pail)
    =/  res=(each manx tang)  (render-udon:sail code)
    :_  pail
    ?-  -.res
      %.y
        :~
          [(welp here.bowl #/manx) %make %manx `manx/!>(+:res) ~]
          [(welp here.bowl #/tang) %cull ~]
        ==
      %.n
        :~
          [(welp here.bowl #/tang) %make %tang `tang/!>(+:res) ~]
          [(welp here.bowl #/manx) %cull ~]
        ==
      ::
    ==
  ++  init
    |=  pal=(unit pail:neo)
    ^-  (quip card:neo pail:neo)
    ?~  pal  `txt/!>(*txt)
    ?>  ~|(incompatible-stud/p.u.pal =(p.u.pal %txt))
    =/  code  !<(txt q.u.pal)
    =/  res=(each manx tang)  (render-udon:sail code)
    :_  u.pal
    ?-  -.res
      %.y
        :~
          [(welp here.bowl #/manx) %make %manx `manx/!>(+:res) ~]
          [(welp here.bowl #/tang) %cull ~]
        ==
      %.n
        :~
          [(welp here.bowl #/tang) %make %tang `tang/!>(+:res) ~]
          [(welp here.bowl #/manx) %cull ~]
        ==
    ==
  --
--
